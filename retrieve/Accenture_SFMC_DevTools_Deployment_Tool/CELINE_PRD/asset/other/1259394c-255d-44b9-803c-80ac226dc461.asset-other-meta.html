%%[
/* Retrieving the Subscriber Key and the Language */
VAR @skey, @preheader, @subject, @mirrorLink

IF EMPTY(_subscriberkey) THEN
    SET @skey = ContactKey
ELSE
    SET @skey = _subscriberkey
ENDIF


/* Retrieving the Language of the Subscriber from the Main Data Extension Contacts */
VAR @language, @regionMainStore

VAR @rows 
SET @rows = LookupRows("Contacts", "ContactKey", @skey)

IF Rowcount(@rows) > 0 THEN
    VAR @row
    SET @row = Row(@rows, 1)
    SET @language = Field(@row, "Language")
    SET @regionMainStore = Field(@row, "Region Main Store")
/* For Proofing we need to get the variables from the Proof Data Extension not from the Contacts DE, because we do not have the same subscribers keys*/ 
ELSE
      SET @rows = LookupRows(_DataSourceName , "ContactKey", @skey)
      IF Rowcount(@rows) > 0 THEN
        VAR @row
        SET @row = Row(@rows, 1)
        SET @language = Field(@row, "Language")
        SET @regionMainStore = Field(@row, "Region Main Store")
    ENDIF
ENDIF

/* Retrieving the subject and the Preheader from the Data Extension EMAIL_SUBJECT_PREHEADER_TRANSLATION */
IF NOT EMPTY(@language) AND @regionMainStore=="US" THEN 

    VAR @rows
    SET @rows = LookupRows("EMAIL_SUBJECT_PREHEADER_TRANSLATION", "Language", "US")
    
    VAR @rowSelected
    SET @rowSelected = Row(@rows, 1)
    
    SET @preheader = Field(@rowSelected, "Preheader")
    SET @subject = Field(@rowSelected, "Subject")
    SET @mirrorLink = Field(@rowSelected, "Mirror Link")

ELSEIF NOT EMPTY(@language) THEN

    VAR @rows
    SET @rows = LookupRows("EMAIL_SUBJECT_PREHEADER_TRANSLATION", "Language", @language)
    
    VAR @rowSelected
    SET @rowSelected = Row(@rows, 1)
    
    SET @preheader = Field(@rowSelected, "Preheader")
    SET @subject = Field(@rowSelected, "Subject")
    SET @mirrorLink = Field(@rowSelected, "Mirror Link")
ELSE

    VAR @rows
    SET @rows = LookupRows("EMAIL_SUBJECT_PREHEADER_TRANSLATION", "Language", "Default")
    
    VAR @rowSelected
    SET @rowSelected = Row(@rows, 1)
    
    SET @preheader = Field(@rowSelected, "Preheader")
    SET @subject = Field(@rowSelected, "Subject")
    SET @mirrorLink = Field(@rowSelected, "Mirror Link")

ENDIF

]%%
